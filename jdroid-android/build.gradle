apply plugin: 'com.jdroid.android.library'
apply plugin: 'jacoco'

ext.projectName = 'Jdroid Android'
description = 'Jdroid dependency project for Android apps'

def googlePlayServicesVersion = '7.5.0'

repositories {
	maven {
		url "http://download.crashlytics.com/maven"
	}
}

buildscript {

	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.jdroidframework:jdroid-gradle-plugin:' + project.version
	}
}

configurations {
	compile.exclude group: "org.apache.httpcomponents",  module: "httpclient"
	compile.exclude group: "org.slf4j",  module: "slf4j-api"
}

configurations.all {
	// check for updates every build
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {

	// https://github.com/nostra13/Android-Universal-Image-Loader
	compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'

	compile 'org.slf4j:slf4j-android:1.6.1-RC1'
	compile 'com.crashlytics.android:crashlytics:1.1.13'
	// https://developers.facebook.com/docs/android
	compile('com.facebook.android:facebook-android-sdk:3.21.1') {
		exclude module: 'support-v4'
	}

	compile 'com.android.support:support-v4:22.2.0'
	compile 'com.android.support:recyclerview-v7:22.2.0'
	compile 'com.android.support:appcompat-v7:22.2.0'
	compile 'com.android.support:cardview-v7:22.2.0'
	compile 'com.android.support:multidex:1.0.1'

	compile 'com.google.android.gms:play-services-ads:' + googlePlayServicesVersion
	compile 'com.google.android.gms:play-services-analytics:' + googlePlayServicesVersion
	compile 'com.google.android.gms:play-services-base:' + googlePlayServicesVersion
	compile 'com.google.android.gms:play-services-gcm:' + googlePlayServicesVersion
	compile 'com.google.android.gms:play-services-identity:' + googlePlayServicesVersion
	compile 'com.google.android.gms:play-services-location:' + googlePlayServicesVersion
	compile 'com.google.android.gms:play-services-maps:' + googlePlayServicesVersion
	compile 'com.google.android.gms:play-services-plus:' + googlePlayServicesVersion

	// https://github.com/facebook/device-year-class
	compile 'com.facebook.device.yearclass:yearclass:1.0.1'

	// Module dependencies
	compile project(':jdroid-java')

	testCompile 'junit:junit:4.12'
	testCompile "org.mockito:mockito-all:1.10.19"

	// https://github.com/robolectric/robolectric
	testCompile('org.robolectric:robolectric:2.4') {
		exclude module: 'classworlds'
		exclude module: 'commons-logging'
		exclude module: 'httpclient'
		exclude module: 'maven-artifact'
		exclude module: 'maven-artifact-manager'
		exclude module: 'maven-error-diagnostics'
		exclude module: 'maven-model'
		exclude module: 'maven-project'
		exclude module: 'maven-settings'
		exclude module: 'plexus-container-default'
		exclude module: 'plexus-interpolation'
		exclude module: 'plexus-utils'
		exclude module: 'wagon-file'
		exclude module: 'wagon-http-lightweight'
		exclude module: 'wagon-provider-api'
	}
}

jdroid {
	notDefaultLanguages = ['es']
}

android {

	lintOptions {
		disable 'ContentDescription', 'RtlEnabled', 'RtlHardcoded', 'RtlSymmetry', 'UseCompoundDrawables', 'UnknownIdInLayout'
	}

	// TODO We will publish the debug version until we discover how to publish both debug and release
	//publishNonDefault true
	defaultPublishConfig "debug"

	sourceSets.test.java.srcDirs += "build/generated/source/r/debug"
	sourceSets.test.java.srcDirs += sourceSets.main.java.srcDirs
	sourceSets.test.java.srcDirs += sourceSets.debug.java.srcDirs

	// TODO This is causing errors when loading the app
//	buildTypes {
//		debug {
//			testCoverageEnabled = true
//		}
//	}
}

task androidSourcesJar(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.sourceFiles, android.sourceSets.debug.java.sourceFiles
}

artifacts {
	archives androidSourcesJar
}

jacoco {
	toolVersion = "0.7.4.201502262128"
}

// http://raptordigital.blogspot.com.ar/2014/08/code-coverage-reports-using-robolectric.html
task jacocoTestReport(type:JacocoReport, dependsOn: "testDebug") {
	group = "Reporting"
	description = "Generate Jacoco coverage reports"

	def excludesFilter = ['**/R.class',
						  '**/R$*.class',
						  '**/*$ViewInjector*.*',
						  '**/BuildConfig.*',
						  '**/Manifest*.*']
	def includesFilter = ['**/com/jdroid/android/*.class',
						  '**/com/jdroid/android/maps/**/*.class',
						  '**/com/jdroid/android/context/**/*.class',
						  '**/com/jdroid/android/http/**/*.class',
						  '**/com/jdroid/android/domain/**/*.class',
						  '**/com/jdroid/android/utils/**/*.class',
						  '**/com/jdroid/android/api/**/*.class',
						  '**/com/jdroid/android/uri/**/*.class',
						  '**/com/jdroid/android/exception/**/*.class',
						  '**/com/jdroid/android/analytics/**/*.class']
	def debugTree = fileTree(dir: "${project.buildDir}/intermediates/classes/debug", includes: includesFilter, excludes: excludesFilter)
	classDirectories = files([debugTree])

	def mainSrc = "${project.projectDir}/src/main/java"
	sourceDirectories = files([mainSrc])

	executionData = fileTree(dir: project.projectDir, includes: ['**/*.exec', '**/*.ec'])

	reports {
		xml.enabled = true
		html.enabled = true
	}
}
