apply plugin: 'jetty'
apply plugin: 'com.jdroid.java'

description = 'Jdroid Sample Server'
version = '0.7.0-SNAPSHOT'

repositories {
	mavenCentral()
	maven {
		url "http://dl.dropbox.com/u/8520947/maven-repository/"
	}
}

buildscript {
	repositories {
		mavenCentral()
		maven {
			url "http://dl.dropbox.com/u/8520947/maven-repository/"
		}
	}
}

//configurations.all {
//	resolutionStrategy.cacheChangingModulesFor 30, 'seconds'
//}

configurations {
	runtimeOnly
}

buildscript {
	dependencies {
		classpath 'com.jdroidframework:jdroid-gradle-plugin:0.7.0-SNAPSHOT'
	}
}

dependencies {
	compile 'com.jdroidframework:jdroid-javaweb:0.7.0-SNAPSHOT'

	// to run our App on Heroku
	runtimeOnly "org.mortbay.jetty:jetty-runner:8.1.1.v20120215"
}

// Generate the settings.properties
project.afterEvaluate {

	project.task('generateSettings') << {
		new File("$projectDir/src/main/resources/settings.properties").withWriter { out ->
			out.writeLine("appName=${project.name}")
			out.writeLine("appVersion=${project.version}")
			out.writeLine("buildTime=${jdroid.buildTime()}")
			out.writeLine("googleServerApiKey=${jdroid.getProp('googleServerApiKey')}")
			out.writeLine("httpMockEnabled=${jdroid.getProp('httpMockEnabled', false)}")
			out.writeLine("httpMockSleepDuration=${jdroid.getProp('httpMockSleepDuration', 10)}")
		}
	}

	project.tasks.'processResources'.dependsOn 'generateSettings'

}

task copyJettyRunner << {
	copy {
		from configurations.runtimeOnly.copy().setTransitive(false)
		into "$buildDir/libs"
		rename { name ->
			def artifacts = configurations.runtimeOnly.resolvedConfiguration.resolvedArtifacts
			def artifact = artifacts.find { it.file.name == name }
			"${artifact.name}.${artifact.extension}"
		}
	}
}

tasks.assemble.dependsOn 'copyJettyRunner'